       IDENTIFICATION DIVISION.
	   PROGRAM-ID. STOK1.
       ENVIRONMENT DIVISION.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT STOK1 ASSIGN TO DISK ORGANIZATION IS INDEXED
           ACCESS MODE IS DYNAMIC RECORD KEY IS STKNO.
           SELECT YAZICI ASSIGN TO PRINTER.
       DATA DIVISION.
       FILE SECTION.
       FD STOK1 LABEL RECORDS ARE STANDARD VALUE OF
           FILE-ID "B:STOK1.DOS".
       01 STOK-KAYDI.
           05 STKNO PIC X(7).
           05 MALIN-ISMI PIC X(20).
           05 MIN-MIKTAR PIC 9(4).
           05 MEVCUT PIC 9(4).
           05 MAX-MIKTAR PIC 9(4).
           05 BIRIM-MALIYET PIC 9(7).
           05 SATIS-FIATI PIC 9(7).
           05 KDV-ORANI PIC 9V99.
       FD YAZICI LABEL RECORDS ARE OMITTED.
       01 CIKTI-SATIRI PIC X(80).
       WORKING-STORAGE SECTION.
       77 K PIC 99 VALUE ZEROS.
       77 CEVAP PIC X.
       77 UYARI-1 PIC X(65).
	   77 STD PIC X(7).
       77 UYARI-2 PIC X(65).
       77 BOSLUK PIC X VALUE SPACE.
       77 TPMEV PIC 9(6).
       77 TPENV PIC 9(8).
       77 GTPMEV PIC 9(7).
       77 GTPENV PIC 9(9).
       77 ENV PIC 9(7).
       77 ESCKOD PIC 99.
       77 MVC-ILAVE PIC 9(4).
       77 BSN PIC X(7).
       77 SSN PIC X(7).
       77 N PIC 99.
       01 ATAMA.
           05 ATAMA-1 PIC X(33).
           05 ATAMA-2 PIC X(33).
       01 BAS1.
           05 F PIC X(34) VALUE SPACES.
           05 F PIC X(31) VALUE "STOK-KONTROL".
           05 F PIC X(10) VALUE "SAYFA NO:".
           05 SAYFANO PIC 99.
	   01 AS1.
	       05 F PIC X(80) VALUE SPACES.	   
       01 BAS2.
           05 F PIC X(34) VALUE SPACES.
           05 F PIC X(12) VALUE ALL "-".
       01 BAS3.
           05 F PIC X(3) VALUE SPACES.
           05 F PIC X(6) VALUE "SIRA".
           05 F PIC X(9) VALUE "STOK NO".
           05 F PIC X(22) VALUE "MALIN ISMI".
           05 F PIC X(6) VALUE "MNMV".
           05 F PIC X(6) VALUE "MVCT".
           05 F PIC X(6) VALUE "MXMC".
           05 F PIC X(7) VALUE "DURUM".
           05 F PIC X(12) VALUE "ENV DEGERI".
       01 BAS4.
           05 F PIC X(3) VALUE SPACES.
           05 F PIC X(74) VALUE ALL "-".
       01 BAS5.
           05 F PIC X(28) VALUE SPACES.
           05 F PIC X(14) VALUE "SAYFA TOPLAMI".
           05 F PIC XX VALUE SPACES.
           05 TOPLAM-MEV PIC Z(5)9.
           05 F PIC X(15) VALUE SPACES.
           05 TOPLAM-ENV PIC ZZ,ZZZ,ZZ9.
           05 F PIC XX VALUE "-".
       01 BAS6.
           05 F PIC X(28) VALUE SPACES.
           05 F PIC X(14) VALUE "GENEL TOPLAM :".
           05 F PIC X VALUE SPACE.
           05 GENEL-MEV PIC Z(6)9.
		   05 F PIC X(15) VALUE SPACES.
		   05 GENEL-ENV PIC ZZ,ZZZ,ZZ9.
           05 F PIC XX VALUE "-".
       01 DETAY.
           05 F PIC XXX VALUE SPACES.
           05 SIRA PIC 9999.
           05 F PIC XX VALUE SPACES.
           05 D-STOKNO PIC X(7)BB.
           05 D-MALISMI PIC X(20)BB.
           05 D-MINMEV PIC ZZZ9BB.
           05 D-MEVCUT PIC ZZZ9BB.
           05 D-MAXMEV PIC ZZZ9BB.
           05 DURUM PIC X(6)B.
           05 D-ENVANTER PIC ZZ,ZZZ,ZZ9.
           05 F PIC XX VALUE "-".
       SCREEN SECTION.
       01 EKRAN1.
           05 LINE 1 COLUMN 1 REVERSE-VIDEO VALUE "BEBIM A.S.".
           05 LINE 2 COLUMN 1 REVERSE-VIDEO VALUE "ANA MENU".
           05 LINE 3 COLUMN 1 PIC X(9) FROM ALL "-".
           05 LINE 5 COLUMN 11 PIC X(50) FROM ALL "-".
           05 LINE 6 COLUMN 10 VALUE
              "******* STOK KONTROL PROGRAMI *******".
           05 LINE 7 COLUMN 10 PIC X(50) FROM ALL "-".
           05 LINE 8 COLUMN 10 VALUE
              "  1...STOK MAMULLERI GIRISI          ".
           05 LINE 9 COLUMN 10 VALUE
              "  2...STOK BILGILERINDE DEGISIKLIK   ".
           05 LINE 10 COLUMN 10 VALUE
              "  3...ISTENILEN BIR KAYDIN SILINMESI ".
           05 LINE 11 COLUMN 10 VALUE
              "  4...SIP. VEYA IADELERIN ISLENMESI  ". 
           05 LINE 12 COLUMN 10 VALUE
              "  5...STOKTAN MAL CIKISI ISLENMESI   ".
           05 LINE 13 COLUMN 10 VALUE
              "  6...STOK KONTROL ** EKRANDAN **    ".
           05 LINE 14 COLUMN 10 VALUE
              "  7...STOK KONTROL ** YAZICIDAN **   ".
           05 LINE 15 COLUMN 10 VALUE
            "                                     ".
           05 LINE 16 COLUMN 10 VALUE
              "  8...ANA PROGRAMA DONUS             ".
           05 LINE 17 COLUMN 10 VALUE "*".
           05 LINE 17 COLUMN 11 PIC X(50) FROM ALL "-".
           05 LINE 17 COLUMN 60 VALUE "*".
           05 LINE 18 COLUMN 10 VALUE
              "       ***** SECIMINIZ *****         ".
           05 LINE 19 COLUMN 11 PIC X(50) FROM ALL "-".
       01 EKRAN2.
           05 LINE 21 COLUMN 10 PIC 99 USING K.
           05 LINE 21 COLUMN 13 BLINK VALUE
              ". NO'LU ISLEM TAMAMLANMISTIR".
       01 EKRAN3.
           05 LINE 21 COLUMN 10 PIC 99 USING K.
           05 LINE 21 COLUMN 13 BLINK VALUE
              " . NO'LU SECENEK YANLIS LUTFEN DOGRUSUNU GIRINIZ".
       01 STK-EKRAN-1.
           05 BLANK SCREEN.	       
           05 LINE 1 COLUMN 1 VALUE "BEBIM A.S.".
           05 LINE 2 COLUMN 1 VALUE "STOK KONTROL".
           05 LINE 3 COLUMN 1 PIC X(12) FROM ALL "-".
           05 LINE 6 COLUMN 10 PIC X(40) FROM ALL "=".
          05 LINE 7 COLUMN 10 VALUE
              "STOK NUMARASI     :".
           05 LINE 7 COLUMN 30 PIC X(7) USING STKNO.
       01 STK-EKRAN-2. 
           05 LINE 9 COLUMN 10 VALUE
              "MALIN ISMI        :".
           05 LINE 9 COLUMN 30 PIC X(20) USING MALIN-ISMI.
           05 LINE 10 COLUMN 10 VALUE "MIN.STOK MIKTARI.:".
           05 LINE 10 COLUMN 30 PIC Z(3)9 USING MIN-MIKTAR. 
           05 LINE 11 COLUMN 10 VALUE "MEVCUT MIKTAR....:".
           05 LINE 11 COLUMN 30 PIC Z(3)9 USING MEVCUT.
           05 LINE 12 COLUMN 10 VALUE "MAX.STOK MIKTARI.:".
           05 LINE 12 COLUMN 30 PIC Z(3)9 USING MAX-MIKTAR.
           05 LINE 13 COLUMN 10 VALUE "BIRIM MALIYETI...:".
           05 LINE 13 COLUMN 30 PIC Z(4)9 USING BIRIM-MALIYET.
           05 LINE 14 COLUMN 10 VALUE "SATIS FIAT.......:".
           05 LINE 14 COLUMN 30 PIC Z(4)9 USING SATIS-FIATI.
           05 LINE 15 COLUMN 10 VALUE "KDV ORANI........:".
           05 LINE 15 COLUMN 30 PIC 9.99 USING KDV-ORANI.
       01 STK-EKRAN-3.
           05 LINE 16 COLUMN 10 VALUE "ENVANTER DEGERI  :".
           05 LINE 16 COLUMN 30 PIC Z,ZZZ,ZZ9 USING ENV.
           05 LINE 17 COLUMN 10 PIC X(40) FROM ALL "-".
       01 STK-EKRAN-4.
           05 LINE 23 COLUMN 1 VALUE "UYARI-1:".
           05 LINE 23 COLUMN 10 PIC X(65) USING UYARI-1.
           05 LINE 24 COLUMN 1 VALUE "UYARI-2:".
           05 LINE 24 COLUMN 10 PIC X(65) USING UYARI-2.
       PROCEDURE DIVISION.
       BASLA. DISPLAY (1 1) ERASE. 
	       DISPLAY EKRAN1.
       10. ACCEPT (18 35) K WITH PROMPT.
           IF K = 99 OPEN OUTPUT STOK1 CLOSE STOK1
           ELSE IF K = 8 GO STOK-SON
           ELSE IF K = 1 PERFORM GIRIS
           ELSE IF K = 2 PERFORM DEGISIKLIK
           ELSE IF K = 3 PERFORM SILME
           ELSE IF K = 4 PERFORM SIPARIS-IADE
           ELSE IF K = 5 PERFORM MAL-CIKISI
           ELSE IF K = 6 PERFORM EKRAN-STOK
           ELSE IF K = 7 PERFORM YAZICI-STOK
           ELSE DISPLAY EKRAN3 GO 10.
           DISPLAY (1 1) ERASE DISPLAY EKRAN1 EKRAN2 GO 10.
       GIRIS SECTION.
       AA0. OPEN I-O STOK1.
       AA1. MOVE 0 TO MIN-MIKTAR MEVCUT MAX-MIKTAR BIRIM-MALIYET
           SATIS-FIATI KDV-ORANI.
           MOVE ALL "." TO STKNO MALIN-ISMI.
           MOVE SPACES TO UYARI-1 UYARI-2.
           DISPLAY STK-EKRAN-1.
           DISPLAY (5 10) "STOK MAMULLERI GIRISI".
           DISPLAY (24 1) "{ESC} ILE ANA MENUYE DONERSINIZ".
           ACCEPT STK-EKRAN-1 ON ESCAPE GO GIRIS-SON.
           READ STOK1 INVALID KEY GO AA2.
           MULTIPLY MEVCUT BY BIRIM-MALIYET GIVING ENV.
           MOVE "BU KAYIT ONCEDEN GIRILMIS" TO UYARI-1.
           MOVE "BASKA BIR KAYIT ICIN {ENT} BASINIZ" TO UYARI-2.
           DISPLAY (23 1) ERASE.
           DISPLAY STK-EKRAN-2 STK-EKRAN-3 STK-EKRAN-4.
           ACCEPT (24 50) CEVAP GO AA1.
       AA2. MOVE "CTRL B ILE BIR ALAN GERI GIDILIR" TO UYARI-1.
           MOVE "{ESC} ILE ANA MENUYE DONERSINIZ" TO UYARI-2.
           DISPLAY (23 1) ERASE.
           DISPLAY STK-EKRAN-2 STK-EKRAN-4.
           ACCEPT STK-EKRAN-2 ON ESCAPE GO GIRIS-SON.
           MULTIPLY MEVCUT BY BIRIM-MALIYET GIVING ENV.
           MOVE SPACES TO UYARI-2 UYARI-1.
           DISPLAY (23 1) ERASE. MOVE "VERILER DOGRU MU (E/H):"  TO
           UYARI-1. DISPLAY STK-EKRAN-4 STK-EKRAN-3.
           ACCEPT (23 34) CEVAP WITH PROMPT.
           IF CEVAP = "H" GO AA2.
           WRITE STOK-KAYDI INVALID KEY
           MOVE "DISKET BOZUK VEYA DOLU" TO UYARI-1
           MOVE "PROGRAMCINIZA DANISINIZ {ENT} BAS" TO UYARI-2
           DISPLAY STK-EKRAN-4 ACCEPT (24 43) CEVAP GO GIRIS-SON.
           GO AA1.
       GIRIS-SON. CLOSE STOK1.
       DEGISIKLIK SECTION.
       BB0. OPEN I-O STOK1.
       BB1. MOVE ALL "." TO STKNO STD.
           DISPLAY STK-EKRAN-1.
           DISPLAY (5 10) "STOK BILGILERINDE DEGISIKLIK".
           MOVE "DEGISIKLIK YAPACAGINIZ KAYDIN STOK NO'SUNU GIRINIZ"
           TO UYARI-1
           MOVE "{ESC} BASINCA ANA MENUYE DONERSINIZ:"  TO UYARI-2.
           DISPLAY STK-EKRAN-4. 
           ACCEPT STK-EKRAN-1 ON ESCAPE GO DEGISIKLIK-SON.
		   MOVE STKNO TO STD.
       DE. READ STOK1 INVALID KEY MOVE SPACES TO UYARI-1
           MOVE "BU STOK NUMARASI ILE DOSYADA KAYIT YOK {ENT} BAS:"
           TO UYARI-2 DISPLAY STK-EKRAN-4 ACCEPT (24 60) CEVAP
           GO BB1.
		   IF STD NOT = STKNO GO DE.
           DISPLAY STK-EKRAN-2.
           MOVE "{ESC} BASINCA BIR ALAN GERI GIDERSINIZ" TO UYARI-1.
           MOVE SPACES TO UYARI-2.
           DISPLAY STK-EKRAN-4.
       BB3. ACCEPT (9 30) MALIN-ISMI WITH PROMPT UPDATE.
       BB4. ACCEPT (10 30) MIN-MIKTAR WITH PROMPT UPDATE.
           ACCEPT ESCKOD FROM ESCAPE KEY.
           IF ESCKOD = 1 GO BB3.
       BBC. ACCEPT (11 30) MEVCUT WITH PROMPT UPDATE.
           ACCEPT ESCKOD FROM ESCAPE KEY.
           IF ESCKOD = 1 GO BB4.		   
       BB5. ACCEPT (12 30) MAX-MIKTAR WITH PROMPT UPDATE.
           ACCEPT ESCKOD FROM ESCAPE KEY.
           IF ESCKOD = 1 GO BB4.
       BB6. ACCEPT (13 30) BIRIM-MALIYET WITH PROMPT UPDATE.
           ACCEPT ESCKOD FROM ESCAPE KEY.
           IF ESCKOD = 1 GO BB5.
       BB7. ACCEPT (14 30) SATIS-FIATI WITH PROMPT UPDATE.
           ACCEPT ESCKOD FROM ESCAPE KEY.
           IF ESCKOD = 1 GO BB6.
       BB77. ACCEPT (15 30) KDV-ORANI WITH PROMPT UPDATE.
           ACCEPT ESCKOD FROM ESCAPE KEY.
           IF ESCKOD = 1 GO BB7.
           MULTIPLY MEVCUT BY BIRIM-MALIYET GIVING ENV.
           MOVE "KAYDI DOSYAYA YAZMAK ISTIYORMUSUNUZ {E/H}:" TO
           UYARI-2.
           DISPLAY STK-EKRAN-3 STK-EKRAN-4.
           ACCEPT (24 52) CEVAP WITH PROMPT.
           ACCEPT ESCKOD FROM ESCAPE KEY.
           IF ESCKOD = 1 GO BB77.
           IF CEVAP = "H" GO BB1.
           REWRITE STOK-KAYDI INVALID KEY
           MOVE "DISKET BOZUK" TO UYARI-1
           MOVE "ANA MENUYE DONMEK ICIN {ENT} BAS:" TO UYARI-2
           DISPLAY (23 1) ERASE DISPLAY STK-EKRAN-4
           ACCEPT (24 48) CEVAP GO DEGISIKLIK-SON. GO BB1.
       DEGISIKLIK-SON. CLOSE STOK1.
       SILME SECTION.
       CC0. OPEN I-O STOK1.
       CC1. MOVE ALL "." TO STKNO STD.
           MOVE SPACES TO UYARI-1 UYARI-2.
           DISPLAY STK-EKRAN-1.
           DISPLAY (5 10) "ISTENILEN BIR KAYDIN SILINMESI".
           MOVE "SILMEK ISTEDIGINIZ STOK NO'YU GIRINIZ:" TO UYARI-1.
           MOVE "{ESC} BASARSANIZ ANA MENUYE DONERSINIZ" TO UYARI-2.
           DISPLAY STK-EKRAN-4.
           ACCEPT STK-EKRAN-1 ON ESCAPE GO SILME-SON.
		   MOVE STKNO TO STD.
       SI. READ STOK1 INVALID KEY MOVE SPACES TO UYARI-1
           MOVE "BU KAYIT DOSYADA MEVCUT DEGIL {ENT} BAS" TO UYARI-2
           DISPLAY STK-EKRAN-4 ACCEPT (24 52) CEVAP GO CC1.
		   IF STD NOT = STKNO GO SI.
           DISPLAY STK-EKRAN-2.
           MULTIPLY MEVCUT BY BIRIM-MALIYET GIVING ENV.
           DISPLAY STK-EKRAN-3.
           MOVE "BU KAYDI SILMEK ISTEDIGINIZDEN EMINMISINIZ (E/H):"
           TO UYARI-2. MOVE SPACES TO UYARI-1.
           DISPLAY (23 1) ERASE.
           DISPLAY STK-EKRAN-4.
           ACCEPT (24 60) CEVAP WITH PROMPT.
           IF CEVAP = "H" GO CC1.
           DELETE STOK1 RECORD INVALID KEY
           MOVE "**KAYDI SILEMIYORUM**DISKET BOZUK** " TO UYARI-1.
           MOVE "ANA MENUYE DONMEK ICIN {ENT} BAS:" TO UYARI-2
           DISPLAY (23 1) ERASE DISPLAY STK-EKRAN-4
           ACCEPT (24 45) CEVAP GO SILME-SON. GO CC1.
       SILME-SON. CLOSE STOK1.
       SIPARIS-IADE SECTION.
       DD0. OPEN I-O STOK1.
       DD1. MOVE ALL "." TO STKNO STD.
           MOVE SPACES TO UYARI-1 UYARI-2.
           MOVE ZEROS TO MVC-ILAVE.
           MOVE "ILAVE YAPILACAK STOK NO'YU GIRINIZ:" TO UYARI-1.
           MOVE "{ESC} BASARSANIZ ANA MENUYE DONERSINIZ" TO UYARI-2.
           DISPLAY STK-EKRAN-1 STK-EKRAN-4.
           DISPLAY (5 10) "SIPARIS VEYA IADELERIN ISLENMESI".
           ACCEPT STK-EKRAN-1 ON ESCAPE GO SIPARIS-IADE-SON.
		   MOVE STKNO TO STD.
       SIPT. READ STOK1 INVALID KEY MOVE SPACES TO UYARI-1
           MOVE "BU KAYIT MEVCUT DEGIL {ENT} BAS" TO UYARI-2
           DISPLAY (23 1) ERASE DISPLAY STK-EKRAN-4
           ACCEPT (24 52) CEVAP GO DD1.
		   IF STD NOT = STKNO GO SIPT.
           MULTIPLY MEVCUT BY BIRIM-MALIYET GIVING ENV.
           DISPLAY STK-EKRAN-2 STK-EKRAN-3.
           DISPLAY (19 10) "**********************************".
           DISPLAY (20 10) "** ILAVE MIKTARINI GIRINIZ:     **".
           DISPLAY (21 10) "**********************************".
       DD2. ACCEPT (20 41) MVC-ILAVE WITH PROMPT.
           ADD MVC-ILAVE TO MEVCUT.
           MULTIPLY MEVCUT BY BIRIM-MALIYET GIVING ENV.
           MOVE "YAPILAN SIPARIS/IADE MIKTARI DOGRUMU {E/H}:"
           TO UYARI-2. MOVE SPACES TO UYARI-1. DISPLAY (23 1) ERASE.
           DISPLAY STK-EKRAN-2 STK-EKRAN-3 STK-EKRAN-4.
           ACCEPT (24 55) CEVAP WITH PROMPT.
           IF CEVAP = "H" SUBTRACT MVC-ILAVE FROM MEVCUT GO DD2.
           REWRITE STOK-KAYDI INVALID KEY 
           MOVE "** YAZAMIYORUM ** DISKET BOZUK **" TO UYARI-1
           MOVE "ANA MENUYE DONMEK ICIN {ENT} BAS" TO UYARI-2
           DISPLAY (23 1) ERASE DISPLAY STK-EKRAN-4
           ACCEPT (24 45) CEVAP GO SIPARIS-IADE-SON. GO DD1.
       SIPARIS-IADE-SON. CLOSE STOK1.
       MAL-CIKISI SECTION.
       EE0. OPEN I-O STOK1.
       EE1. MOVE ALL "." TO STKNO.
           MOVE SPACES TO UYARI-1 UYARI-2.
           MOVE ZEROS TO MVC-ILAVE.
           MOVE "CIKIS YAPILACAK MALIN STOK NO'SUNU GIRINIZ:"
           TO UYARI-1.
           MOVE "{ESC} BASARSANIZ ANA MENUYE DONERSINIZ" TO UYARI-2.
           DISPLAY STK-EKRAN-1 STK-EKRAN-4.
           ACCEPT STK-EKRAN-1 ON ESCAPE GO MAL-CIKISI-SON.
           READ STOK1 INVALID KEY MOVE SPACES TO UYARI-1
           MOVE "BU KAYIT DOSYADA MECVUT DEGIL {ENT} BAS" TO UYARI-2
           DISPLAY(23 1) ERASE DISPLAY STK-EKRAN-4
           ACCEPT (24 52) CEVAP GO EE1.
           MULTIPLY MEVCUT BY BIRIM-MALIYET GIVING ENV.
           DISPLAY STK-EKRAN-2  STK-EKRAN-3.
           DISPLAY (19 10) "************************************".
           DISPLAY (20 10) "*** CIKIS MIKTARINI GIRINIZ:     ***".
           DISPLAY (21 10) "************************************".
       EE2. ACCEPT (20 41) MVC-ILAVE WITH PROMPT.
           SUBTRACT MVC-ILAVE FROM MEVCUT.
           MULTIPLY MEVCUT BY BIRIM-MALIYET GIVING ENV.
           MOVE "YAPILAN MAL CIKIS MIKTARI DOGRU MU {E/H}:" TO UYARI-2.
           DISPLAY (23 1) ERASE. MOVE SPACES TO UYARI-1.
           DISPLAY STK-EKRAN-2 STK-EKRAN-3 STK-EKRAN-4.
           ACCEPT (24 52) CEVAP WITH PROMPT.
           IF CEVAP = "H" ADD MVC-ILAVE TO MEVCUT GO EE2.
           REWRITE STOK-KAYDI INVALID KEY
           MOVE "** YAZAMIYORUM ** DISKET BOZUK **" TO UYARI-1
           MOVE "ANA MENUYE DONMEK ICIN {ENT} BAS" TO UYARI-2
           DISPLAY STK-EKRAN-4 ACCEPT (24 52) CEVAP 
           GO MAL-CIKISI-SON. GO EE1.
       MAL-CIKISI-SON. CLOSE STOK1.
       EKRAN-STOK SECTION.
       TGD. MOVE 1 TO LIN.
       HH0. DISPLAY (1 1) ERASE.
           MOVE ALL "." TO BSN SSN.
           DISPLAY (2 1) "BEBIM A.S.".
           DISPLAY (8 10) "** STOK KONTROL ** EKRANDA **".
           DISPLAY (9 10) "=========================".
           DISPLAY (11 10) "BASLANGIC STOK NO :".
           DISPLAY (13 10) "SON STOK NO       :".
           DISPLAY (15 10) "========================".
           DISPLAY (23 1) "BAS.STOKNO=0000000 VE SON-STOKNO=".
           DISPLAY (23 34)
            "9999999 GIRERSEN HEPSINI DOKERSIN".
       HH1. ACCEPT (11 31) BSN WITH PROMPT UPDATE.
           ACCEPT (13 31) SSN WITH PROMPT UPDATE.
           DISPLAY (24 1) "VERILER DOGRUMU {E/H} :".
           ACCEPT (24 35) CEVAP WITH PROMPT.
           IF CEVAP = "H" DISPLAY (24 1) ERASE GO HH1.
           OPEN INPUT STOK1.
           MOVE ZEROS TO GTPMEV GTPENV SAYFANO SIRA.
           MOVE BSN TO STKNO.
           START STOK1 KEY NOT LESS STKNO INVALID KEY DISPLAY (24 1)
           "DOSYADA START YAPILAMIYOR ENT BAS:" ACCEPT (24 46) CEVAP GO
           EKRAN-STOK-SON.
           READ STOK1 NEXT RECORD AT END GO EKRAN-STOK-SON.
           IF STKNO > SSN GO EKRAN-STOK-SON.
           PERFORM EKRAN-BASLIKLAR.
       HH2. ADD 1 TO SIRA. ADD 1 TO LIN.
           MULTIPLY MEVCUT BY BIRIM-MALIYET GIVING ENV.
           MOVE ENV TO D-ENVANTER. MOVE STKNO TO D-STOKNO.
           MOVE MALIN-ISMI TO D-MALISMI. MOVE MIN-MIKTAR TO D-MINMEV.
           MOVE MAX-MIKTAR TO D-MAXMEV. MOVE MEVCUT TO D-MEVCUT.
           IF MEVCUT < MIN-MIKTAR MOVE "AZ" TO DURUM
           ELSE IF MEVCUT > MAX-MIKTAR MOVE "COK" TO DURUM
           ELSE MOVE "NORMAL" TO DURUM.
           DISPLAY (LIN + 4 1) DETAY. ADD MEVCUT TO TPMEV.
           ADD ENV TO TPENV. 
           READ STOK1 NEXT AT END PERFORM EKRAN-SONUC GO EKRAN-STOK-SON.
           IF STKNO > SSN PERFORM EKRAN-SONUC GO EKRAN-STOK-SON.
           IF LIN < 15 GO HH2.
           PERFORM EKRAN-SONUC. DISPLAY (24 1)
           "EKRAN DOKUMUNE DEVAM ETMEK ISTIYORMUSUNUZ {E/H} :".
           ACCEPT (24 48) CEVAP WITH PROMPT UPDATE.
           IF CEVAP = "H" GO STOK-SON ELSE PERFORM EKRAN-BASLIKLAR.
           GO HH2.
       EKRAN-BASLIKLAR.
           ADD 1 TO SAYFANO. DISPLAY (1 1) ERASE.
           DISPLAY (1 1) BAS1. DISPLAY (2 1) BAS2.
           DISPLAY (3 1) BAS3. DISPLAY (4 1) BAS4.
           MOVE 0 TO LIN TPMEV TPENV.
       EKRAN-SONUC.
           ADD TPMEV TO GTPMEV. ADD TPENV TO GTPENV.
           MOVE TPMEV TO TOPLAM-MEV. 
           MOVE TPENV TO TOPLAM-ENV.
           MOVE GTPMEV TO GENEL-MEV. 
           MOVE GTPENV TO GENEL-ENV.
           DISPLAY (LIN + 5 1) BAS4. DISPLAY (LIN + 6 1) BAS5.
           DISPLAY (LIN + 7 1) BAS6.
       EKRAN-STOK-SON.
           DISPLAY (24 1) "ANA MENUYE DONMEK ICIN {ENT} BAS".
           ACCEPT (24 36) CEVAP WITH PROMPT. CLOSE STOK1.
       YAZICI-STOK SECTION.
       FF0. OPEN I-O STOK1 OUTPUT YAZICI.          
	       DISPLAY (1 1) ERASE. MOVE ALL "." TO BSN SSN.
           MOVE SPACES TO ATAMA.
           MOVE "BAS.STOKNO=0000000 VE SON-STOKNO=" TO ATAMA-1.
           MOVE "9999999 GIRERSEN HEPSINI DOKERSINIZ" TO ATAMA-2.
           MOVE ATAMA TO UYARI-1. MOVE SPACES TO UYARI-2.
           DISPLAY (1 1) "BEBIM A.S.".
           DISPLAY (2 1) "STOK KONTROL".
           DISPLAY (8 14) "## STOK KONTROL ## YAZICIDA ##".
           DISPLAY (9 10) "&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&".
           DISPLAY (11 10) "BASLANGIC STOK NO   :".
           DISPLAY (13 10) "SON STOK NO         :".
           DISPLAY (15 10) "&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&".
           DISPLAY STK-EKRAN-4.
       FF1. ACCEPT (11 33) BSN WITH PROMPT UPDATE.
           ACCEPT (13 33) SSN WITH PROMPT UPDATE.
           DISPLAY (24 10) "VERILER DOGRU MU [E/H]:".
           ACCEPT (24 35) CEVAP WITH PROMPT.
           IF CEVAP = "H" DISPLAY (24 10) ERASE GO FF1.
           MOVE ZEROS TO GTPMEV GTPENV SAYFANO SIRA.
           MOVE BSN TO STKNO. MOVE SPACES TO UYARI-1 UYARI-2.
           DISPLAY (23 1) ERASE.
           START STOK1 KEY IS NOT LESS THAN STKNO INVALID KEY
           MOVE "DOSYADA ## START ## YAPILAMIYOR" TO UYARI-1
           MOVE "ANA MENUYE DONMEK ICIN {ENT} BASINIZ" TO UYARI-2
           DISPLAY (23 1) ERASE DISPLAY STK-EKRAN-4
           ACCEPT (24 37) CEVAP GO YAZICI-STOK-SON.
           READ STOK1 NEXT RECORD AT END
           GO YAZICI-STOK-SON.
           IF STKNO > SSN GO YAZICI-STOK-SON. PERFORM BASLIKLAR.
       FF2. ADD 1 TO SIRA. ADD 1 TO N.
           MULTIPLY MEVCUT BY BIRIM-MALIYET GIVING ENV.
           MOVE STKNO TO D-STOKNO. MOVE MALIN-ISMI TO D-MALISMI.
           MOVE MIN-MIKTAR TO D-MINMEV. MOVE MEVCUT TO D-MEVCUT.
           MOVE MAX-MIKTAR TO D-MAXMEV. MOVE ENV TO D-ENVANTER.
           IF MEVCUT < MIN-MIKTAR
           MOVE "AZ" TO DURUM
           ELSE IF MEVCUT > MAX-MIKTAR
           MOVE "COK" TO DURUM
           ELSE MOVE "NORMAL" TO DURUM.
           WRITE CIKTI-SATIRI FROM DETAY AFTER 1.
           ADD MEVCUT TO TPMEV. ADD ENV TO TPENV.
           READ STOK1 NEXT AT END PERFORM SONUC GO YAZICI-STOK-SON.
           IF STKNO > SSN PERFORM SONUC GO YAZICI-STOK-SON.
           IF N < 50 GO FF2. PERFORM SONUC. 
           MOVE "DOKUME DEVAM ETMEK ISTIYORMUSUNUZ {E/H}:" TO UYARI-2.
           DISPLAY STK-EKRAN-4. ACCEPT (24 50) CEVAP WITH PROMPT.
           IF CEVAP = "H" GO YAZICI-STOK-SON.
           WRITE CIKTI-SATIRI FROM BOSLUK AFTER 4.
           PERFORM BASLIKLAR. GO FF2.
       BASLIKLAR.
           ADD 1 TO SAYFANO.
           WRITE CIKTI-SATIRI FROM BAS1 AFTER 3.
           WRITE CIKTI-SATIRI FROM BAS2 AFTER 1.
           WRITE CIKTI-SATIRI FROM BAS3 AFTER 2.
           WRITE CIKTI-SATIRI FROM BAS4 AFTER 1.
           MOVE 0 TO N TPMEV TPENV.
       SONUC.
           ADD TPMEV TO GTPMEV.
           ADD TPENV TO GTPENV.
           MOVE TPMEV TO TOPLAM-MEV.
           MOVE TPENV TO TOPLAM-ENV.
           MOVE GTPMEV TO GENEL-MEV. 
           MOVE GTPENV TO GENEL-ENV.
           WRITE CIKTI-SATIRI FROM BAS4 AFTER 1.
           WRITE CIKTI-SATIRI FROM BAS5 AFTER 1.
           WRITE CIKTI-SATIRI FROM BAS6 AFTER 1.
       YAZICI-STOK-SON. WRITE CIKTI-SATIRI FROM BOSLUK AFTER 1.
           CLOSE STOK1 YAZICI.
       STOK-SON SECTION.
       I-10. EXIT PROGRAM.

	   